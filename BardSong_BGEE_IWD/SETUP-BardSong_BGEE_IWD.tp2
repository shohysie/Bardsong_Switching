BACKUP ~BardSong_BGEE_IWD/backup~
AUTHOR ~shohy@126.com~

VERSION ~v4.7~
AUTO_EVAL_STRINGS

README ~BardSong_BGEE_IWD/README.%LANGUAGE%.txt~
AUTO_TRA ~BardSong_BGEE_IWD/tra/%s~

LANGUAGE	~American English~		~english~	~BardSong_BGEE_IWD/tra/english/setup.tra~	~BardSong_BGEE_IWD/tra/english/game.tra~
LANGUAGE	~Chinese Simplified~	~chinese~	~BardSong_BGEE_IWD/tra/chinese/setup.tra~	~BardSong_BGEE_IWD/tra/chinese/game.tra~
LANGUAGE	~French~				~french~	~BardSong_BGEE_IWD/tra/french/setup.tra~	~BardSong_BGEE_IWD/tra/french/game.tra~

////////////////////////////////////////////////////////////////////////////

BEGIN @1
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ OR FILE_EXISTS_IN_GAME ~BD1000.are~ @11

////////////////////////非高阶技能//////////////////////////

//读取kitlist中ability列，确定技能列表的名称
COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_ROWS 10 kitsmax
	FOR (kitrow = 0 ; kitrow < kitsmax ; ++kitrow) BEGIN
		READ_2DA_ENTRY kitrow 1 9 kitname
		PATCH_IF ~%kitname%~ STRING_EQUAL_CASE ~BLADE~ BEGIN
			READ_2DA_ENTRY kitrow 5 9 bladeability
			//PATCH_PRINT  ~%bladeability%~
		END
		PATCH_IF ~%kitname%~ STRING_EQUAL_CASE ~JESTER~ BEGIN
			READ_2DA_ENTRY kitrow 5 9 jesterability
			//PATCH_PRINT  ~%jesterability%~
		END
		PATCH_IF ~%kitname%~ STRING_EQUAL_CASE ~SKALD~ BEGIN
			READ_2DA_ENTRY kitrow 5 9 skaldability
			//PATCH_PRINT  ~%skaldability%~
		END
	END
BUT_ONLY

ACTION_IF NOT (FILE_CONTAINS_EVALUATED (~CLABBA01.2da~ ~AP_BI#B10A~)) BEGIN
	APPEND ~CLABBA01.2da~ ~ABILITYA AP_BI#B10A **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** CDREPLACE
	ABILITYB GA_BI#B11A **** **** GA_BI#B12A **** **** **** GA_BI#B13A **** **** **** GA_BI#B14A **** **** **** GA_BI#B15A **** **** **** GA_BI#B16A CDREPLACE~
	COPY_EXISTING ~CLABBA01.2da~ ~override~
	  COUNT_2DA_COLS cols
	  FOR (index = 22 ; index < cols ; ++index) BEGIN
	    REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
	  END
	  REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
	  PRETTY_PRINT_2DA
END

ACTION_IF NOT (FILE_CONTAINS_EVALUATED (~%bladeability%.2da~ ~AP_BI#B20A~)) BEGIN
	APPEND ~%bladeability%.2da~ ~ABILITYA AP_BI#B20A **** **** **** **** **** **** **** **** AP_SPCL541 **** **** **** **** **** **** **** **** **** AP_SPCL541 CDREPLACE
	ABILITYB GA_BI#B11A **** **** **** **** GA_BI#B12A **** **** **** **** **** GA_BI#B13A **** **** **** **** **** GA_BI#B14A **** **** CDREPLACE~
	COPY_EXISTING ~%bladeability%.2da~ ~override~
	  COUNT_2DA_COLS cols
	  FOR (index = 22 ; index < cols ; ++index) BEGIN
	    REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
	  END
	  REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
	  COUNT_2DA_ROWS 15 rows	//行数
	  FOR (index = 0 ; index < rows ; ++index) BEGIN
		  READ_2DA_ENTRY index 1 15 bladeab1
		  PATCH_IF ("%bladeab1%" STRING_COMPARE_CASE "AP_BI#B20A" = 0) BEGIN
		  	SET "index1" = "index"
		  	SET "index" = "%rows%" + 1
		  END
		END
  	PATCH_IF cols > 50 BEGIN SET_2DA_ENTRY index1 50 51 ~AP_SPCL541~ END
  	PATCH_IF cols > 40 BEGIN SET_2DA_ENTRY index1 40 41 ~AP_SPCL541~ END
  	PATCH_IF cols > 30 BEGIN SET_2DA_ENTRY index1 30 31 ~AP_SPCL541~ END
	  PRETTY_PRINT_2DA
END

ACTION_IF NOT (FILE_CONTAINS_EVALUATED (~%jesterability%.2da~ ~AP_BI#B30A~)) BEGIN
	APPEND ~%jesterability%.2da~ ~ABILITYA AP_BI#B30A **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** CDREPLACE~
	COPY_EXISTING ~%jesterability%.2da~ ~override~
	  COUNT_2DA_COLS cols
	  FOR (index = 22 ; index < cols ; ++index) BEGIN
	    REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
	  END
	  REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
	  PRETTY_PRINT_2DA
END

ACTION_IF NOT (FILE_CONTAINS_EVALUATED (~%skaldability%.2da~ ~AP_BI#B40A~)) BEGIN
	APPEND ~%skaldability%.2da~ ~ABILITYA AP_BI#B40A **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** CDREPLACE~
	COPY_EXISTING ~%skaldability%.2da~ ~override~
	  COUNT_2DA_COLS cols
	  FOR (index = 22 ; index < cols ; ++index) BEGIN
	    REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
	  END
	  REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
	  PRETTY_PRINT_2DA
END

////////////////////////高阶技能//////////////////////////

//读取luabbr.2da中abbrev列，确定高阶技能列表的名称（Ba0等）
COPY_EXISTING ~luabbr.2da~ ~override~
	COUNT_2DA_ROWS 2 kitsmax
	FOR (kitrow = 0 ; kitrow < kitsmax ; ++kitrow) BEGIN
		READ_2DA_ENTRY kitrow 0 2 kitname
		PATCH_IF ~%kitname%~ STRING_EQUAL_CASE ~BARD~ BEGIN
			SET "bardrow" = %kitrow% + 2
			READ_2DA_ENTRY kitrow 1 2 bardabbrev
		END
		PATCH_IF ~%kitname%~ STRING_EQUAL_CASE ~BLADE~ BEGIN
			SET "bladerow" = %kitrow% + 2
			READ_2DA_ENTRY kitrow 1 2 bladeabbrev
		END
		PATCH_IF ~%kitname%~ STRING_EQUAL_CASE ~JESTER~ BEGIN
			SET "jesterrow" = %kitrow% + 2
			READ_2DA_ENTRY kitrow 1 2 jesterabbrev
		END
		PATCH_IF ~%kitname%~ STRING_EQUAL_CASE ~SKALD~ BEGIN
			SET "skaldrow" = %kitrow% + 2
			READ_2DA_ENTRY kitrow 1 2 skaldabbrev
		END
	END
BUT_ONLY

//Blade没有单独高阶技能列表
ACTION_IF ~%bladeabbrev%~ STRING_EQUAL ~%bardabbrev%~ THEN BEGIN
	ACTION_IF FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~Ba1~) THEN BEGIN	//Ba1已经被占用了
		OUTER_FOR (index = 4 ; index < 10 ; ++index) BEGIN
			ACTION_IF NOT FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~Ba%index%~) THEN BEGIN	//在4-9找一个没被占用的
				OUTER_SPRINT bladeabbrev ~Ba%index%~
				OUTER_SET "index" = 10 // kills loop
			END
		END
		ACTION_IF index <10  THEN BEGIN	//什么鬼，5-9全占了，下面几个总能选到一个吧（后者优先）
			ACTION_FOR_EACH ~abbrevname~ IN ~Baa~ ~Bau~ ~Bax~ ~BLD~ ~Bbl~ BEGIN
				ACTION_IF NOT FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~%abbrevname%~) THEN BEGIN
					OUTER_SPRINT bladeabbrev ~%abbrevname%~
				END
			END
		END
	END ELSE BEGIN	//Ba1没被占用，就用它
		OUTER_SPRINT bladeabbrev ~Ba1~
	END
	COPY_EXISTING ~luabbr.2da~ ~override~
		SET_2DA_ENTRY "%bladerow%" 1 1 ~%bladeabbrev%~  // 在luabbr.2da里指定Ba1为Blade的高阶技能
	BUT_ONLY
	COPY_EXISTING ~LU%bardabbrev%.2da~ ~override/LU%bladeabbrev%.2da~
END
//Jester没有单独高阶技能列表
ACTION_IF (~%jesterabbrev%~ STRING_EQUAL ~%bardabbrev%~)||(~%jesterabbrev%~ STRING_EQUAL ~%bladeabbrev%~) THEN BEGIN
	ACTION_IF FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~Ba2~) THEN BEGIN	//Ba2已经被占用了
		OUTER_FOR (index = 4 ; index < 10 ; ++index) BEGIN
			ACTION_IF NOT FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~Ba%index%~) THEN BEGIN	//在4-9找一个没被占用的
				OUTER_SPRINT jesterabbrev ~Ba%index%~
				OUTER_SET "index" = 10 // kills loop
			END
		END
		ACTION_IF index <10  THEN BEGIN	//什么鬼，5-9全占了，下面几个总能选到一个吧（后者优先）
			ACTION_FOR_EACH ~abbrevname~ IN ~Bab~ ~Bav~ ~Bay~ ~JST~ ~Bje~ BEGIN
				ACTION_IF NOT FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~%abbrevname%~) THEN BEGIN
					OUTER_SPRINT jesterabbrev ~%abbrevname%~
				END
			END
		END
	END ELSE BEGIN	//Ba2没被占用，就用它
		OUTER_SPRINT jesterabbrev ~Ba2~
	END
	COPY_EXISTING ~luabbr.2da~ ~override~
		SET_2DA_ENTRY "%jesterrow%" 1 1 ~%jesterabbrev%~  // 在luabbr.2da里指定Ba2为Jester的高阶技能
	BUT_ONLY
	COPY_EXISTING ~LU%bardabbrev%.2da~ ~override/LU%jesterabbrev%.2da~
END
//Skald没有单独高阶技能列表
ACTION_IF (~%skaldabbrev%~ STRING_EQUAL ~%bardabbrev%~)||(~%skaldabbrev%~ STRING_EQUAL ~%bladeabbrev%~)||(~%skaldabbrev%~ STRING_EQUAL ~%jesterabbrev%~) THEN BEGIN
	ACTION_IF FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~Ba3~) THEN BEGIN	//Ba3已经被占用了
		OUTER_FOR (index = 4 ; index < 10 ; ++index) BEGIN
			ACTION_IF NOT FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~Ba%index%~) THEN BEGIN	//在4-9找一个没被占用的
				OUTER_SPRINT skaldabbrev ~Ba%index%~
				OUTER_SET "index" = 10 // kills loop
			END
		END
		ACTION_IF index <10  THEN BEGIN	//什么鬼，5-9全占了，下面几个总能选到一个吧（后者优先）
			ACTION_FOR_EACH ~abbrevname~ IN ~Bac~ ~Baw~ ~Baz~ ~SKD~ ~Bsk~ BEGIN
				ACTION_IF NOT FILE_CONTAINS_EVALUATED (~luabbr.2da~ ~%abbrevname%~) THEN BEGIN
					OUTER_SPRINT skaldabbrev ~%abbrevname%~
				END
			END
		END
	END ELSE BEGIN	//Ba3没被占用，就用它
		OUTER_SPRINT skaldabbrev ~Ba3~
	END
	COPY_EXISTING ~luabbr.2da~ ~override~
		SET_2DA_ENTRY "%skaldrow%" 1 1 ~%skaldabbrev%~  // 在luabbr.2da里指定Ba3为Skald的高阶技能
	BUT_ONLY
	COPY_EXISTING ~LU%bardabbrev%.2da~ ~override/LU%skaldabbrev%.2da~
END

INCLUDE ~BardSong_BGEE_IWD/lib/hlaadd.tpa~
//处理高阶技能列表
LAF ADD_BARD_HLA STR_VAR 2daname = ~LU%bardabbrev%~ hlaenhancedsong = ~AP_BI#B1SS~ hlalingeredsong = ~AP_BI#B1ST~ END
LAF ADD_BARD_HLA STR_VAR 2daname = ~LU%bladeabbrev%~ hlaenhancedsong = ~AP_BI#B2SS~ hlalingeredsong = ~AP_BI#B1ST~ END
LAF ADD_BARD_HLA STR_VAR 2daname = ~LU%jesterabbrev%~ hlaenhancedsong = ~AP_BI#B3SS~ hlalingeredsong = ~AP_BI#B1ST~ END
LAF ADD_BARD_HLA STR_VAR 2daname = ~LU%skaldabbrev%~ hlaenhancedsong = ~AP_BI#B4SS~ hlalingeredsong = ~AP_BI#B1ST~ END

//舞剑士没有小旋风的话，加上
ACTION_IF NOT (FILE_CONTAINS_EVALUATED (~LU%bladeabbrev%.2da~ ~GA_SPCL900~)) BEGIN
//PRINT ~LU%bladeabbrev%.2da不含小旋风~
	COPY_EXISTING_REGEXP ~LU%bladeabbrev%.2da~ ~override~
	COUNT_2DA_ROWS ~9~ "rows"
	FOR ( index = 1 ; index < rows ; index = index + 1 ) BEGIN
		READ_2DA_ENTRY "%index%" 1 9 "abil"
		PATCH_IF ("%abil%" STRING_COMPARE_CASE "*" = 0) BEGIN	//遇到空行，加上小旋风
//PATCH_PRINT ~第%index%行为空行，改成小旋风~
			SET_2DA_ENTRY "%index%" 1 9 ~GA_SPCL900~
			SET_2DA_ENTRY "%index%" 4 9 ~1~
			SET_2DA_ENTRY "%index%" 5 9 ~99~
			SET_2DA_ENTRY "%index%" 6 9 ~20~
			SET "index" = "%rows%" + 1 // kills loop
		END
	END
END

ADD_PROJECTILE      ~BardSong_BGEE_IWD/pro/BI#BINPA.pro~	//对50呎内友军
ADD_PROJECTILE      ~BardSong_BGEE_IWD/pro/BI#BINPL.pro~	//对100呎内友军
ADD_PROJECTILE      ~BardSong_BGEE_IWD/pro/BI#BINNP.pro~	//对50呎内敌军

COPY    ~BardSong_BGEE_IWD/SPL/BI#B11A.SPL~   ~override~
	SAY NAME1 @21    SAY UNIDENTIFIED_DESC @41

COPY    ~BardSong_BGEE_IWD/SPL/BI#B12A.SPL~   ~override~
	SAY NAME1 @22    SAY UNIDENTIFIED_DESC @42

COPY    ~BardSong_BGEE_IWD/SPL/BI#B13A.SPL~   ~override~
	SAY NAME1 @23    SAY UNIDENTIFIED_DESC @43

COPY    ~BardSong_BGEE_IWD/SPL/BI#B14A.SPL~   ~override~
	SAY NAME1 @24    SAY UNIDENTIFIED_DESC @44

COPY    ~BardSong_BGEE_IWD/SPL/BI#B15A.SPL~   ~override~
	SAY NAME1 @25    SAY UNIDENTIFIED_DESC @45

COPY    ~BardSong_BGEE_IWD/SPL/BI#B16A.SPL~   ~override~
	SAY NAME1 @26    SAY UNIDENTIFIED_DESC @46
	
COPY    ~BardSong_BGEE_IWD/SPL/innp~   ~override~	//对敌战歌
	WRITE_SHORT   0x98 ~%BI#BINNP%~

COPY    ~BardSong_BGEE_IWD/SPL/inpa~   ~override~	//对友战歌
	WRITE_SHORT   0x98 ~%BI#BINPA%~

COPY    ~BardSong_BGEE_IWD/SPL/inpl~   ~override~	//对友强力战歌
	WRITE_SHORT   0x98 ~%BI#BINPL%~

COPY    ~BardSong_BGEE_IWD/SPL/BI#B30B.SPL~   ~override~	//小丑普通战歌效果，不需要余音缭绕
	WRITE_SHORT   0x98 ~%BI#BINNP%~
	WRITE_SHORT   0xc0 ~%BI#BINNP%~
	WRITE_SHORT   0xe8 ~%BI#BINNP%~
	WRITE_SHORT   0x110 ~%BI#BINNP%~
	WRITE_SHORT   0x138 ~%BI#BINNP%~
	SAY 0x2ee @7	//迷惑
	SAY 0x43e @7
	SAY 0x58e @7
	SAY 0x5ee @8	//迟缓
	SAY 0x73e @7
	SAY 0x79e @8
	SAY 0x8ee @7
	SAY 0x94e @8
	SAY 0x9ae @9	//昏迷不醒

COPY    ~BardSong_BGEE_IWD/SPL/BI#B40B.SPL~   ~override~	//北地诗人普通战歌效果，不需要余音缭绕
	WRITE_SHORT   0x98 ~%BI#BINPA%~
	WRITE_SHORT   0xc0 ~%BI#BINPA%~
	WRITE_SHORT   0xe8 ~%BI#BINPA%~
	WRITE_SHORT   0x110 ~%BI#BINPA%~
	WRITE_SHORT   0x138 ~%BI#BINPA%~
	
COPY    ~BardSong_BGEE_IWD/SPL/BI#B1SS.SPL~   ~override~
	SAY NAME1 @29    SAY UNIDENTIFIED_DESC @31
COPY    ~BardSong_BGEE_IWD/SPL/BI#B1SA.SPL~   ~override~
	SAY NAME1 @30    SAY UNIDENTIFIED_DESC @31
COPY    ~BardSong_BGEE_IWD/SPL/BI#B1ST.SPL~   ~override~
	SAY NAME1 @39    SAY UNIDENTIFIED_DESC @40

COPY    ~BardSong_BGEE_IWD/SPL/BI#B2SS.SPL~   ~override~
	SAY NAME1 @29    SAY UNIDENTIFIED_DESC @32
COPY    ~BardSong_BGEE_IWD/SPL/BI#B2SA.SPL~   ~override~
	SAY NAME1 @30    SAY UNIDENTIFIED_DESC @32

COPY    ~BardSong_BGEE_IWD/SPL/BI#B3SS.SPL~   ~override~
	SAY NAME1 @29    SAY UNIDENTIFIED_DESC @33
COPY    ~BardSong_BGEE_IWD/SPL/BI#B3SB.SPL~   ~override~
	WRITE_SHORT   0x98 ~%BI#BINNP%~
	READ_SHORT 0x27e ~AIname~
	READ_SHORT 0x27e ~AIdesc~
	SAY 0x27e @7	//迷惑
	SAY 0x2de @8	//迟缓
	SAY 0x33e @9	//昏迷不醒

COPY    ~BardSong_BGEE_IWD/SPL/BI#B4SS.SPL~   ~override~
	SAY NAME1 @29    SAY UNIDENTIFIED_DESC @34

COPY    ~BardSong_BGEE_IWD/SPL/noname~   ~override~
COPY    ~BardSong_BGEE_IWD/bam~         ~override~

//专用AI
COMPILE    ~BardSong_BGEE_IWD/AI/BI#BARD.baf~
MOVE    ~override/BI#BARD.bcs~ ~scripts/BI#BARD.bs~
OUTER_SET AIname = RESOLVE_STR_REF(@3)
OUTER_SET AIdesc = RESOLVE_STR_REF(@4)
APPEND ~SCRPDESC.2da~ ~BI#BARD    %AIname%      %AIdesc%~	

//修改描述
OUTER_SET "bard_desc" = 0
OUTER_SET "blade_desc" = 0
OUTER_SET "jester_desc" = 0
OUTER_SET "skald_desc" = 0

COPY_EXISTING ~clastext.2da~ ~override~
	COUNT_2DA_ROWS 9 rows
	FOR ( index = 0 ; index < rows ; index = index + 1 ) BEGIN
		READ_2DA_ENTRY "%index%" 0 9 "kit_name"
		READ_2DA_ENTRY "%index%" 4 9 "kit_desc"
		PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "BARD" = 0) BEGIN SET "bard_desc" = "kit_desc" END
		PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "BLADE" = 0) BEGIN SET "blade_desc" = "kit_desc" END
		PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "JESTER" = 0) BEGIN SET "jester_desc" = "kit_desc" END
		PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "SKALD" = 0) BEGIN SET "skald_desc" = "kit_desc" END
	END
BUT_ONLY
STRING_SET_EVALUATE bard_desc @100
STRING_SET_EVALUATE blade_desc @101
STRING_SET_EVALUATE jester_desc @102
STRING_SET_EVALUATE skald_desc @103

ACTION_IF FILE_EXISTS_IN_GAME ~bgclatxt.2da~ BEGIN
	COPY_EXISTING ~bgclatxt.2da~ ~override~
		COUNT_2DA_ROWS 9 rows
		FOR ( index = 0 ; index < rows ; index = index + 1 ) BEGIN
			READ_2DA_ENTRY "%index%" 0 9 "kit_name"
			READ_2DA_ENTRY "%index%" 4 9 "kit_desc"
			PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "BARD" = 0) BEGIN SET "bard_desc" = "kit_desc" END
			PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "BLADE" = 0) BEGIN SET "blade_desc" = "kit_desc" END
			PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "JESTER" = 0) BEGIN SET "jester_desc" = "kit_desc" END
			PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "SKALD" = 0) BEGIN SET "skald_desc" = "kit_desc" END
		END
	BUT_ONLY
	STRING_SET_EVALUATE bard_desc @100
	STRING_SET_EVALUATE blade_desc @101
	STRING_SET_EVALUATE jester_desc @102
	STRING_SET_EVALUATE skald_desc @103
END


////////////////////////////////////////////////////////////////////////////

BEGIN @12	//合唱功能组件
REQUIRE_COMPONENT "SETUP-BardSong_BGEE_IWD.tp2" "0" @13

OUTER_SET "splprot_row" = 0
COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_ROWS 4 rows
	FOR ( index = 0 ; index < rows ; index = index + 1 ) BEGIN
		READ_2DA_ENTRY "%index%" 1 4 "splprot_stat"
		READ_2DA_ENTRY "%index%" 2 4 "splprot_value"
		READ_2DA_ENTRY "%index%" 3 4 "splprot_relation"
	  //PATCH_PRINT ~%index% %splprot_stat% %splprot_value% %splprot_relation%~
		PATCH_IF ("%splprot_stat%" STRING_COMPARE_CASE "56" = 0) & ("%splprot_value%" STRING_COMPARE_CASE "-1" = 0) & ("%splprot_relation%" STRING_COMPARE_CASE "1" = 0) BEGIN	//有现成的BACKSTABDAMAGEMULTIPLIER判断行
			SET "splprot_row" = "%index%"
			SET "index" = "%rows%" + 1 // kills loop
		END
	END
	PATCH_IF (splprot_row = 0) BEGIN
		SET "splprot_row" = "%rows%"
		INSERT_2DA_ROW rows 4 ~%rows%_BACKSTABDAMAGEMULTIPLIER=n                56                                            -1                                            1~
	END
BUT_ONLY
//splprot_row为splprot.2da新增BACKSTABDAMAGEMULTIPLIER判断行的编号

ADD_PROJECTILE      ~BardSong_BGEE_IWD/chorus/pro/BI#BBIRD.pro~	//对50呎内非自身的友军

COPY_EXISTING ~BI#B10A.spl~ ~override~	~BI#B20A.spl~ ~override~	 ~BI#B30A.spl~ ~override~	 ~BI#B40A.spl~ ~override~
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 timing = 4 parameter2 = 1 duration = 1 STR_VAR resource = BI#B00D END

COPY    ~BardSong_BGEE_IWD/chorus/bam~         ~override~

COPY    ~BardSong_BGEE_IWD/chorus/cre~         ~override~

COPY    ~BardSong_BGEE_IWD/chorus/eff~         ~override~

COPY    ~BardSong_BGEE_IWD/chorus/itm~         ~override~

COPY    ~BardSong_BGEE_IWD/chorus/spl/BI#B00A.spl~   ~override~
	SAY NAME1 @91    SAY UNIDENTIFIED_DESC @94

COPY    ~BardSong_BGEE_IWD/chorus/spl/BI#B00B.spl~   ~override~
	SAY NAME1 @92    SAY UNIDENTIFIED_DESC @95

COPY    ~BardSong_BGEE_IWD/chorus/spl/BI#B00C.spl~   ~override~
	SAY NAME1 @93    SAY UNIDENTIFIED_DESC @96

COPY    ~BardSong_BGEE_IWD/chorus/spl/BI#B00F.spl~   ~override~
	SAY 0xfe  @16	//我可是自带背景音乐的人物！
	SAY 0x12e @17	//没有人能在我的背景音乐里打败我！
	SAY 0x15e @18	//为了保护费伦，我们需要成为偶像！
	SAY 0x18e @19	//你能跟上我的节奏吗？
	SAY 0x1be @20	//来听我唱歌吧！
	SAY 0x1ee @5	//现在由我来领唱。

COPY    ~BardSong_BGEE_IWD/chorus/spl/BI#B00B.eff~   ~override~
	SAY 0x1c @6	//只有队伍成员可以进行领唱。

COPY    ~BardSong_BGEE_IWD/chorus/spl/BI#B00X.spl~   ~override~
	WRITE_SHORT   0x98 ~%BI#BBIRD%~

COPY    ~BardSong_BGEE_IWD/chorus/spl/inpa~   ~override~
	WRITE_SHORT   0x98 ~%BI#BINPA%~
	
COPY    ~BardSong_BGEE_IWD/chorus/spl/inpl~   ~override~
	WRITE_SHORT   0x98 ~%BI#BINPL%~

COPY    ~BardSong_BGEE_IWD/chorus/spl/innp~   ~override~
	WRITE_SHORT   0x98 ~%BI#BINNP%~

COPY    ~BardSong_BGEE_IWD/chorus/spl/inpa5~   ~override~
	WRITE_SHORT   0x98 ~%BI#BINPA%~
	WRITE_SHORT   0xc0 ~%BI#BINPA%~
	WRITE_SHORT   0xe8 ~%BI#BINPA%~
	WRITE_SHORT   0x110 ~%BI#BINPA%~
	WRITE_SHORT   0x138 ~%BI#BINPA%~

COPY    ~BardSong_BGEE_IWD/chorus/spl/innp5~   ~override~
	WRITE_SHORT   0x98 ~%BI#BINNP%~
	WRITE_SHORT   0xc0 ~%BI#BINNP%~
	WRITE_SHORT   0xe8 ~%BI#BINNP%~
	WRITE_SHORT   0x110 ~%BI#BINNP%~
	WRITE_SHORT   0x138 ~%BI#BINNP%~

COPY    ~BardSong_BGEE_IWD/chorus/spl/prot~   ~override~
	LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 326 parameter2 = %splprot_row% END
	
COPY    ~BardSong_BGEE_IWD/chorus/spl/protinpl~   ~override~
	WRITE_SHORT   0x98 ~%BI#BINPL%~
	LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 318 parameter2 = %splprot_row% END
	
COPY_EXISTING    ~BI#B11A.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @51

COPY_EXISTING    ~BI#B12A.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @52

COPY_EXISTING    ~BI#B13A.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @53

COPY_EXISTING    ~BI#B14A.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @54

COPY_EXISTING    ~BI#B15A.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @55

COPY_EXISTING    ~BI#B16A.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @56
	
COPY_EXISTING    ~BI#B1SA.SPL~   ~override~    ~BI#B1SS.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @57

COPY_EXISTING    ~BI#B2SA.SPL~   ~override~    ~BI#B2SS.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @58

COPY_EXISTING    ~BI#B3SS.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @59

COPY_EXISTING    ~BI#B4SS.SPL~   ~override~
	SAY UNIDENTIFIED_DESC @60

COPY    ~BardSong_BGEE_IWD/chorus/spl/noname~   ~override~

COMPILE    ~BardSong_BGEE_IWD/chorus/baf~

//修改描述
OUTER_SET "bard_desc" = 0
OUTER_SET "blade_desc" = 0
OUTER_SET "jester_desc" = 0
OUTER_SET "skald_desc" = 0
COPY_EXISTING ~clastext.2da~ ~override~
	COUNT_2DA_ROWS 9 rows
	FOR ( index = 0 ; index < rows ; index = index + 1 ) BEGIN
		READ_2DA_ENTRY "%index%" 0 9 "kit_name"
		READ_2DA_ENTRY "%index%" 4 9 "kit_desc"
		PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "BARD" = 0) BEGIN SET "bard_desc" = "kit_desc" END
		PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "BLADE" = 0) BEGIN SET "blade_desc" = "kit_desc" END
		PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "JESTER" = 0) BEGIN SET "jester_desc" = "kit_desc" END
		PATCH_IF ("%kit_name%" STRING_COMPARE_CASE "SKALD" = 0) BEGIN SET "skald_desc" = "kit_desc" END
	END
BUT_ONLY
STRING_SET_EVALUATE bard_desc @110
STRING_SET_EVALUATE blade_desc @111
STRING_SET_EVALUATE jester_desc @112
STRING_SET_EVALUATE skald_desc @113

////////////////////////////////////////////////////////////////////////////

BEGIN @14	//允许投影术制造的诗人幻像进行演奏（以及合唱）

COPY_EXISTING ~MISLEAD.spl~ ~override~
	LPF DELETE_EFFECT INT_VAR match_opcode = 144 match_parameter2 = 10 END
	BUT_ONLY

////////////////////////////////////////////////////////////////////////////